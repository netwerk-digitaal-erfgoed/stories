PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <http://schema.org/>
PREFIX som: <http://data.muziekschatten.nl/som/>

select distinct ?eventStart  ?eventLabel (concat(GROUP_CONCAT(distinct ?omschrijving; separator="; " ),
        "; <br> Bron: <a href=", ?url, ">Muziekschatten</a>") as ?eventDescription)
{
  # selecteer composities
  ?s a schema:MusicComposition .

  # zet de titel in het label van op de tijdlijn
  ?s schema:name ?titel .

  # haal het them veld op hierop gaan we straks filteren
  ?s som:SXTHEM ?thema .

  # haal de url naar de muziekschatten pagina op
  ?s schema:url ?url .

  # bepaal de publicatie datum uit één van de twee datum velden
  ?s schema:dateIssued|schema:datePublished ?date .
  # maak een schoon jaar uit de datum velden (negeer ongeldige datums)
  BIND( str(year(?date)) as ?eventStart)
  FILTER( datatype(?date) = xsd:date)
  
  # in het keywords veld staat uitgebreiden informatie dit voegen samen
  # tot de beschrijving van het evenement via de group_concat in de select hierboven
  ?s schema:keywords ?omschrijving .

  # filter de resultaten op het thema songfestival
  filter(contains(lcase(?thema),"songfestival"))

  # probeer nu aan de hand van het performer veld een afbeelding
  # op te halen uit de nl.dbpedia data
  
  # de performer is een zelfstandig resource met eigenschappen
  ?s schema:performer ?performer .

  # sla de naam van de artiest op
  ?performer schema:name ?naam .
  
  BIND(CONCAT(?titel," | ",?naam) as ?eventLabel)
}
# groepeer de resultaten zodat de distinct boven goed werkt.
group by ?eventStart ?eventLabel ?url

# en zet dit in de volgorde van tijd
order by ?eventStart
